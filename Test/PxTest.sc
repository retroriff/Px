/*
TODO: Hide post messages when testing
*/

PxTest : UnitTest {
    var expectedResult;

    setUp {
        Px.lastPatterns = Dictionary.new;
        Px([
            (i: \bd).amp(0.5),
            (i: \sn, dur: 1/4),
        ]);
    }

    tearDown {
        Px.stop;
        Ndef.clear;
        Pdef.clear;
        TempoClock.default.tempo = 110 / 60;
    }

    // Params
    test_playPx {
        expectedResult = Dictionary[(\px -> [
            (i: \bd, amp: 0.5),
            (i: \sn, dur: 0.25)
        ])];

        this.ifAsserts(
            Px.lastPatterns == expectedResult,
            "ðŸ‘€ Patterns are correctly generated",
            this.assert(
                Ndef(\px).isPlaying,
                "ðŸ‘€ Ndef(\\px) is playing"
            );
            this.assert(
                Px.nodeProxy.size > 0,
                "ðŸ‘€ Dictionary nodeProxy is not empty"
            );
        );
    }

    test_currentName {
        Px([(i: \bd)], \test);

        this.assertEquals(
            Px.currentName,
            \test,
            "ðŸ‘€ Px has a custom name",
        );
    }

    test_quant {
        this.assertEquals(
            Pdef(\px).quant,
            4,
            "ðŸ‘€ Default quant is 4",
        );

        Px([(i: \bd)], quant: 8);

        this.assertEquals(
            Pdef(\px).quant,
            8,
            "ðŸ‘€ Custom quant is received",
        );
    }


    // Methods
    test_chorus {
        Px([(i: \bd).amp(0.5)]).save;
        Px.chorus;
        expectedResult = [(i: \bd, amp: 0.5)];

        this.assertEquals(
            Px.chorusPatterns,
            expectedResult,
            "ðŸ‘€ Chorus is saved",
        );
    }

    test_loadSamples {
        this.assert(
            Px.samplesDict.size > 0,
            "ðŸ‘€ Samples dictionary has been initialized by set up file",
        );
    }

    test_release {
        Px.release(0);

        this.assertEquals(
            Ndef(\px).isPlaying,
            false,
            "ðŸ‘€ New seed is saved",
        );
    }

    test_shuffle {
        Px([(i: \bd).beat]).shuffle;

        this.assert(
            Px.seeds.size > 0,
            "ðŸ‘€ New seed is saved",
        );
    }

    test_tempo {
        Px.tempo(60);

        this.assertEquals(
            TempoClock.tempo,
            1,
            "ðŸ‘€ Tempo has been set",
        );
    }

    test_vol {
        this.assertEquals(
            Ndef(\px).vol,
            1,
            "ðŸ‘€ Default volume is 1",
        );

        expectedResult = 0.5;
        Px.vol(expectedResult);

        this.assertEquals(
            Ndef(\px).vol,
            expectedResult,
            "ðŸ‘€ Volume is 0.5",
        );
    }
}
